/*
	CSDUL_Node1_CCOFOG_and_Macroeconomic_Indicator_Data_Processing
			> Overview:
					>> Section 1: Setup
					>> Section 2: Data loading 
					>> Section 3: Data cleaning and merging 
					
			> Datasets:
					>> Macroeconomic indicators
						- Gini coefficient
							> Adjusted after-tax income
							> Source: Statistics Canada
								- Table: 11-10-0134-01
						- Unemployment rate
							> Among those aged 15 years and over
							> Source: Statistics Canada
								- Table: 14-10-0327-01
						- Median after-tax income
							> Base year is 2023 or in 2023 constant dollars
							> Source: Statistics Canada
								- Table: 11-10-0237-01 
						- Gross domestic product - GDP at market prices
							> In 1,000,000 dollars
							> Nominal/Current dollars
							> Source: Statistics Canada
								- Table: 36-10-0221-01 
						- Consumer price index - CPI all-items
							> Base year is 2002 or 2002 constant dollars
							> Source: Statistics Canada
								- Table: 18-10-0005-01
						- Consumer price index - CPI general governments final consumption expenditure
							> Base year is 2017 or in 2017 constant dollars
							> Source: Statistics Canada
								- Table: 36-10-0223-01
					>> Canadian Classification of Functions of Government - CCOFOG
						- Government expenditures classified by the use
						- In 1,000,000 dollars
						- Current dollars - not adjusted for inflation
						- Source: Statistics Canada
						
						
			Note: All datasets/variables can be found by
						a. searching the table name (for example: Table: 11-10-0134-01) 
							on Statistics Canada's official website: https://www.statcan.gc.ca/en/start
							OR
						b. copying and pasting the corresponding URL of each dataset/variable into a browser
							- Note: For instructions on obtaining the corresponding URLs listed in Section 2, 
										please see the separate Word doc ("CSDUL_Node1_StatCan_Data_Acess_Guid.docx")
										
			Note: By running this DO FILE, you will be able to update, load, clean, merge and save all 
						datasets/varaibles into one big dta/xlsx data file for further analysis
						
			Note: All variables are sourced directly from Statistics Canada's public datasets â€” none are 
						calculated or generated in this process
			
			Note: Current dollars or the term nominal suggests that the values are expressed in the actual prices of the year they were recorded - not adjusted for inflation
			
			Note: Constant dollars or the term real suggests that the values are adjusted for inflation to reflect the purchasing power in a base year
*/
	/**********************************************************************************************************************************/
	/*
		SECTION 1: Setup - current year & data source url
	*/
/**********************************************************************************************************************************/
	/* Set to current year */
	global current_year = substr("`c(current_date)'", -4, 4)
	
	/* Set general web url */
	global StatCan "https://www150.statcan.gc.ca/t1/tbl1/en/dtl!downloadDbLoadingData.action?"
	

	
	
	/**********************************************************************************************************************************/
	/*
		SECTION 2: Data Loading - Macroeconomic Indicators & CCOFOG
	*/
/**********************************************************************************************************************************/
	/*
	  Secontion 2.1: Macroeconomic Indicators:
	*/
	/* Gini Coefficient */
	copy "${StatCan}pid=1110013401&latestN=0&startDate=20080101&endDate=${current_year}0101&csvLocale=en&selectedMembers=%5B%5B7%2C8%2C13%5D%2C%5B3%5D%5D&checkedLevels=0D3" "Gini_coefficient.csv", replace

	/* Unemployment Rate */
	copy "${StatCan}pid=1410032701&latestN=0&startDate=20080101&endDate=${current_year}0101&csvLocale=en&selectedMembers=%5B%5B%5D%2C%5B8%5D%2C%5B%5D%2C%5B1%5D%5D&checkedLevels=0D2%2C2D1" "Unemployment_rate.csv", replace
	
	/* Median After-tax Income */
	copy "${StatCan}pid=1110023701&latestN=0&startDate=20080101&endDate=${current_year}0101&csvLocale=en&selectedMembers=%5B%5B7%2C8%2C13%5D%2C%5B3%5D%2C%5B1%5D%2C%5B20%5D%5D&checkedLevels=0D3" "Median_after_tax_income.csv", replace

	/* Gross Domestic Product (GDP) */
	copy "${StatCan}pid=3610022101&latestN=0&startDate=20080101&endDate=${current_year}0101&csvLocale=en&selectedMembers=%5B%5B2%2C3%2C4%2C5%2C6%2C7%2C8%2C9%2C10%2C11%5D%2C%5B14%5D%5D&checkedLevels=" "GDP.csv", replace

	/* Consumer Price Index - All Item */
	copy "${StatCan}pid=1810000501&latestN=0&startDate=20080101&endDate=${current_year}0101&csvLocale=en&selectedMembers=%5B%5B3%2C5%2C7%2C9%2C11%2C14%2C18%2C20%2C23%2C26%5D%2C%5B2%5D%5D&checkedLevels=" "CPI.csv", replace
	
	/* Consumer Price Index - General governments final consumption expenditure */
	copy "${StatCan}pid=3610022301&latestN=0&startDate=20080101&endDate=${current_year}0101&csvLocale=en&selectedMembers=%5B%5B2%2C3%2C4%2C5%2C6%2C7%2C8%2C9%2C10%2C11%5D%2C%5B1%5D%2C%5B9%5D%5D&checkedLevels=" "CPI_govt_exp.csv", replace

	
	/*************************************************************************/
	/*
		Secontion 2.1: CCOFOG
	*/
	
	/* CCOFOG */
	copy "${StatCan}pid=1010000501&latestN=0&startDate=20080101&endDate=${current_year}0101&csvLocale=en&selectedMembers=%5B%5B2%2C3%2C4%2C5%2C6%2C7%2C8%2C9%2C10%2C11%5D%2C%5B2%5D%2C%5B%5D%5D&checkedLevels=2D1%2C2D2" "CCOFOG.csv", replace
	
	
	
	
	/**********************************************************************************************************************************/
	/*
		SECTION 3: Data Cleaning and Merging 
	*/
/**********************************************************************************************************************************/
	/* 
		Section 3.1: Macroeconomic indicators
				> Load, clean and save
	*/
	foreach indicator in "Gini_coefficient" "Unemployment_rate" "Median_after_tax_income" "GDP" "CPI" "CPI_govt_exp" ///
			{
				dis "`indicator'" 						/* Display the current indicator that is being processed */
				
				import delimited "`indicator'", clear   /* Import the downloaded dataset */
				
				keep geo ref_date value 				/* Keep only province, year, and key variable */
				
				rename value `indicator' 				/* Rename the key var to its name */
				
				encode geo, gen(province) 				/* Destring province variable - geo */
				drop geo 
				
				rename ref_date year 					/* Rename */
				
				/* Save each excel spreadsheet in the form of Stata file - dta */
				qui: save "${pathway}`indicator'.dta", replace
	}
	
	/* Merge */
	foreach indicator_saved in "Gini_coefficient" "Unemployment_rate" "Median_after_tax_income" "GDP" "CPI" "CPI_govt_exp" {
				
					merge 1:1 year province using "${pathway}`indicator_saved'.dta"
					drop _merge
			}
	
	/* Sort the dataset by province and year */
	sort province year
	
	/* Reorder */
	order year province Gini_coefficient Unemployment_rate Median_after_tax_income GDP CPI CPI_govt_exp
	
	/* Save the dataset */
	export excel "Macroeconomic_indicators.xlsx", firstrow(variables) replace 
	save "Macroeconomic_indicators.dta", replace
	
	
	/*************************************************************************/
	/* 
		Section 3.2: CCOFOG
				> Load, clean, reshape, rename and save
	*/
	import delimited "CCOFOG.csv", clear
	
	/* Keep only necessary variables */
	keep geo ref_date value canadianclassificationoffunction
	
	/* Destring province variable - geo */
	encode geo, gen(province)
	drop geo
	
	rename ref_date year
	
	gen code = substr(canadianclassificationoffunction, strpos(canadianclassificationoffunction, "[") + 1, strpos(canadianclassificationoffunction, "]") - strpos(canadianclassificationoffunction, "[") - 1)
	
	encode code, gen(code_new)

	tab code_new canadianclassificationoffunction
	drop canadianclassificationoffunction code 
	
	/* Reshape the dataset - from long to wide */
	reshape wide value, i(province year) j(code_new)
	
	/* Rename variables */
	rename value1 	general_public_services_701
	rename value2 	gps_7011
	rename value3 	foreign_economic_aid_7012
	rename value4 	general_services_7013
	rename value5 	basic_esearch_7014
	rename value6 	public_debt_transactions_7017
	rename value7   gps_nec_7015_16_18
	
	rename value8 	defence_702
	rename value9 	military_defence_7021
	rename value10 	civil_defence_7022
	rename value11 	foreign_military_aid_7023
	rename value12  defence_nec_7024_25
	
	rename value13  public_order_and_safety_703
	rename value14  police_services_7031
	rename value15  fire_protection_services_7032
	rename value16  law_courts_7033
	rename value17  prisons_7034
	rename value18  pos_nec_7035_36
	
	rename value19  economic_affairs_704
	rename value20  general_ecl_affairs_7041
	rename value21  agri_fore_fish_hunt_7042
	rename value22  fuel_and_energy_7043
	rename value23  mining_manu_construction_7044
	rename value24  transport_7045
	rename value25	economic_affairs_nec_7046to7049
	
	rename value26  environmental_protection_705
	rename value27  waste_management_7051
	rename value28  waster_water_management_7052
	rename value29  pollution_abatement_7053
	rename value30  protection_of_bandl_7054
	rename value31  envir_protection_nec_7055_56
	
	rename value32  housing_and_commu_amenities_706
	rename value33  h_and_c_development_7061_62
	rename value34  water_supply_7063
	rename value35  street_lighting_7064
	rename value36  h_and_c_ammenities_nec_7065_66
	
	
	rename value37  health_707
	rename value38  med_pae_7071 /* Medical products, appliances, and equipment [7071] */
	rename value39  outpatient_services_7072
	rename value40  hospital_services_7073
	rename value41  public_health_services_7074
	rename value42  health_nec_7075_76
	
	rename value43  rcr_708 /* Recreation, culture and religion */
	rename value44  r_and_sporting_services_7081 /* Recreational and sporting services */
	rename value45  cultural_services_7082
	rename value46	b_and_p_services_7083 /* Broadcasting and publishing services */
	rename value47  rcr_nec_7084to7086 /* Recreation, culture, and religion not elsewhere classified */
	
	rename value48  education_709
	rename value49  primary_and_secondary_edu_7092
	rename value50	college_education_7093
	rename value51	university_education_7094
	rename value52	education_nec_7095to7098
	
	rename value53	social_protection_710
	rename value54	sickness_and_disability_7101_03
	rename value55  old_age_7102
	rename value56	family_and_children_7104
	rename value57	unemployement_7105
	rename value58  housing_7106
	rename value59	social_exclusion_7107
	rename value60	social_protection_nec_7108_09
	
	describe

	/* Save the cleaned dataset in two forms (xlsx and dta) */
	export excel "CCOFOG.xlsx", firstrow(variables) replace 
	save "CCOFOG.dta", replace
	
	/*************************************************************************/
	/*
		Section 3.3: Merging Macroeconomic_indicators.dta with CCOFOG.dta
	*/
	merge 1:1 year province using "Macroeconomic_indicators.dta"
	drop _merge
	
	/* Sort the dataset by province and year*/
	sort province year
	
	/* Save the final dataset in two forms (xlsx and dta) */
	save "Macroeconomic_indicators_and_CCOFOG.dta", replace
	export excel "Macroeconomic_indicators_and_CCOFOG.xlsx", replace
	
	/*
	tab province 
	tab year
	describe
	*/

